<html>
  <head>

    <script type="text/javascript" src="gl-matrix/common.js"></script>
    <script type="text/javascript" src="gl-matrix/mat3.js"></script>
    <script type="text/javascript" src="gl-matrix/mat4.js"></script>
    <script type="text/javascript" src="gl-matrix/vec2.js"></script>
    <script type="text/javascript" src="gl-matrix/vec3.js"></script>
    <script type="text/javascript" src="gl-matrix/vec4.js"></script>
    <script type="text/javascript" src="gl-matrix/quat.js"></script>

    <script type="text/javascript" src="controller.js"></script>
    <script type="text/javascript" src="glutils.js"></script>
    <script type="text/javascript" src="script.js"></script>
    <script type="text/glsl" id="vertex_shader">#version 300 es

      uniform mat4 mMat;
      uniform mat4 vMat;
      uniform mat4 projMat;
      uniform mat4 normMat;

      uniform sampler2D heightTexture; // (height, normX, normY) RGB32F

      uniform int res;
      uniform vec3 patchPos;
      uniform float scale;
      uniform float radius;

      out vec3 modelPos;
      out vec3 normal;
      out float debugScale;
      out vec4 debug;

      void main() {
        int stride = 2 * res + 2;
        int col = gl_VertexID % stride;
        int row = (gl_VertexID - col) / stride;
        int odd = col % 2;
        col /= 2;
        int extra = int(col > res - 1);
        int e1 = (1-odd)*extra;
        int e2 = odd*extra;
        int nx = col - e1 - res*e2;
        int ny = row + odd + e1;

        vec2 texCoords = vec2(nx, ny) / float(res - 1);
        vec2 coords = -.5 + texCoords;

        vec4 heightNormal = texelFetch(heightTexture, ivec2(nx, ny), 0);

        debug = heightNormal;
        float z = heightNormal.x;
        //z /= scale; // Needed becasue bigger tiles are scaled in all 3 dimensionswith mvMat
        // TODO: Do we need to alter the normal becasue of scaled z?
        debugScale = scale;

        vec4 norm4 = vec4(normalize(heightNormal.yzw), 1.);
        norm4 = normMat * norm4;
        normal = norm4.xyz / norm4.w;

        modelPos = vec3(coords, radius / scale);
        vec4 worldPos = mMat * vec4(modelPos, 1.);
        worldPos /= worldPos.w;
        vec3 upVec = normalize(worldPos.xyz);
        worldPos.xyz = (radius + z)*upVec;
        gl_Position = projMat * vMat * worldPos;
      }
    </script>
    <script type="text/glsl" id="fragment_shader">#version 300 es
      precision mediump float;

      in vec3 modelPos;
      in vec3 normal;
      in float debugScale;
      in vec4 debug;

      out vec4 color;

      void main() {
        vec3 n = normalize(normal);
        vec3 lightDir = vec3(0., .707, .707);
        float diffuse = 0.3 + .7* max(0.0, dot(n, lightDir));

        float r = abs(1. - debugScale);
        float g = fract(debugScale);
        float b = fract((r - g) * 100.) + fract(0.1 * debugScale) + fract(0.01 * debugScale);
        vec3 col = vec3(.8, .8, .1); //clamp(vec3(r, g, b), 0., 1.);

        color = vec4(diffuse * col/*debug.rgb*/, 1.);
      }
    </script>
    <script type="text/glsl" id="rtt_vertex_shader">#version 300 es

      void main() {
        int nx = gl_VertexID % 2;
        int ny = (gl_VertexID - nx) / 2;

        gl_Position = vec4(-1. + 2. * vec2(nx, ny), 0., 1.);
      }
    </script>
    <script type="text/glsl" id="rtt_fragment_shader">#version 300 es
      precision mediump float;

      uniform int res;
      uniform vec3 patchPos;
      uniform float scale;

      uniform float radius;
      uniform mat3 normMat;

      out vec4 color;

      float snoise(vec3 v); // 3D Simplex noise
      float snoise(vec3 v, out vec3 grad);

      void main() {

        vec2 texCoords = (gl_FragCoord.xy - .5) / float(res - 1);
        vec2 coords = -.5 + texCoords;

        vec3 world = vec3(scale * coords + patchPos.xy, radius);
        world = radius * normalize(normMat * world); // Rotate and project onto sphere

        vec3 grad = vec3(0.);
        vec3 gradb = vec3(0.);
        vec3 grad2 = vec3(0.);
        float z = 2.+2.*snoise(.1*world, grad);
        float zb = 4.*4.*snoise(.05*world, gradb);
        float z2 = 4.*snoise(.01*world, grad2);
        grad *= .1*2.;
        gradb *= .05*4.;
        //grad += gradb;
        //z += zb;
        grad2 *= .01*4.;

        float lim = 0.6;
        float sm = .2;
        if(z2 < lim) {
          float s = (max(sm*lim, z2) - sm*lim) / ((1.-sm)*lim);
          z2 = sm*lim + s*(1.-sm)*lim;
          grad2 *= s;
        }

        grad = grad * z2 + grad2 * z + grad * grad2;
        z *= z2;
                  

        vec3 normal = vec3(0., 0., 1.); // Normal of base surface (z)
        normal =  grad - dot(grad, normal) * normal + normal;
        normal = normalize(normal);

        color = vec4(z, normal);
      }
    </script>
    <script type="text/glsl" id="simplex2d_shader">
      // Simplex 2D noise
      //
      vec3 permute(vec3 x) { return mod(((x*34.0)+1.0)*x, 289.0); }

      float snoise(vec2 v){
        const vec4 C = vec4(0.211324865405187, 0.366025403784439,
                 -0.577350269189626, 0.024390243902439);
        vec2 i  = floor(v + dot(v, C.yy) );
        vec2 x0 = v -   i + dot(i, C.xx);
        vec2 i1;
        i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
        vec4 x12 = x0.xyxy + C.xxzz;
        x12.xy -= i1;
        i = mod(i, 289.0);
        vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
        + i.x + vec3(0.0, i1.x, 1.0 ));
        vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy),
          dot(x12.zw,x12.zw)), 0.0);
        m = m*m ;
        m = m*m ;
        vec3 x = 2.0 * fract(p * C.www) - 1.0;
        vec3 h = abs(x) - 0.5;
        vec3 ox = floor(x + 0.5);
        vec3 a0 = x - ox;
        m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );
        vec3 g;
        g.x  = a0.x  * x0.x  + h.x  * x0.y;
        g.yz = a0.yz * x12.xz + h.yz * x12.yw;
        return 130.0 * dot(m, g);
      }
    </script>
    <script type="text/glsl" id="simplex3d_shader">
      //  Simplex 3D Noise 
      //  by Ian McEwan, Ashima Arts
      //
      vec4 permute(vec4 x){return mod(((x*34.0)+1.0)*x, 289.0);}
      vec4 taylorInvSqrt(vec4 r){return 1.79284291400159 - 0.85373472095314 * r;}

      // Gradiant added by betelge

      float snoise(vec3 v, out vec3 grad) { 
        const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
        const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

      // First corner
        vec3 i  = floor(v + dot(v, C.yyy) );
        vec3 x0 =   v - i + dot(i, C.xxx) ;

      // Other corners
        vec3 g = step(x0.yzx, x0.xyz);
        vec3 l = 1.0 - g;
        vec3 i1 = min( g.xyz, l.zxy );
        vec3 i2 = max( g.xyz, l.zxy );

        //  x0 = x0 - 0. + 0.0 * C 
        vec3 x1 = x0 - i1 + 1.0 * C.xxx;
        vec3 x2 = x0 - i2 + 2.0 * C.xxx;
        vec3 x3 = x0 - 1. + 3.0 * C.xxx;

      // Permutations
        i = mod(i, 289.0 ); 
        vec4 p = permute( permute( permute( 
                   i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
                 + i.y + vec4(0.0, i1.y, i2.y, 1.0 )) 
                 + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

      // Gradients
      // ( N*N points uniformly over a square, mapped onto an octahedron.)
        float n_ = 1.0/7.0; // N=7
        vec3  ns = n_ * D.wyz - D.xzx;

        vec4 j = p - 49.0 * floor(p * ns.z *ns.z);  //  mod(p,N*N)

        vec4 x_ = floor(j * ns.z);
        vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

        vec4 x = x_ *ns.x + ns.yyyy;
        vec4 y = y_ *ns.x + ns.yyyy;
        vec4 h = 1.0 - abs(x) - abs(y);

        vec4 b0 = vec4( x.xy, y.xy );
        vec4 b1 = vec4( x.zw, y.zw );

        vec4 s0 = floor(b0)*2.0 + 1.0;
        vec4 s1 = floor(b1)*2.0 + 1.0;
        vec4 sh = -step(h, vec4(0.0));

        vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
        vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

        vec3 p0 = vec3(a0.xy,h.x);
        vec3 p1 = vec3(a0.zw,h.y);
        vec3 p2 = vec3(a1.xy,h.z);
        vec3 p3 = vec3(a1.zw,h.w);

      //Normalise gradients
        vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
        p0 *= norm.x;
        p1 *= norm.y;
        p2 *= norm.z;
        p3 *= norm.w;

      // Mix final noise value and gradient
        vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
        vec4 m1 = m;
        m = m * m;
        vec4 m3 = m * m1;
        vec4 gr = vec4( dot(p0,x0), dot(p1,x1),
                        dot(p2,x2), dot(p3,x3) );
        vec4 px = vec4(p0.x, p1.x, p2.x, p3.x); // Can all this be repalced by matrix muls?
        vec4 xx = vec4(x0.x, x1.x, x2.x, x3.x);
        vec4 py = vec4(p0.y, p1.y, p2.y, p3.y);
        vec4 yy = vec4(x0.y, x1.y, x2.y, x3.y);
        vec4 pz = vec4(p0.z, p1.z, p2.z, p3.z);
        vec4 zz = vec4(x0.z, x1.z, x2.z, x3.z);
        grad.x = 42.0 * dot(m3, m1*px-8.*gr*xx);
        grad.y = 42.0 * dot(m3, m1*py-8.*gr*yy);
        grad.z = 42.0 * dot(m3, m1*pz-8.*gr*zz);
        return 42.0 * dot( m*m, gr );
      }

      float snoise(vec3 v) {
        vec3 grad = vec3(0.);
        return snoise(v, grad);
      }
    </script>
  </head>
  <style>
    body {
        margin: 0px;
    }
  </style>
  <body margins="0" onload="start()">
    <canvas id="canvas" width="640" height="480">
      Couldn't initialize canvas element.
    </canvas>
  </body>
</html>
